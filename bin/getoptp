#!/usr/bin/env perl

# A replacement for shell getopt that respects long options on those machines
# where the shell getopt does not. Only implements the most used options.

use strict;
use warnings;

use Getopt::Long qw(:config pass_through auto_abbrev);

## Options to getoptp
my %metaopt =
  ( 'o' => '',
    'l' => '',
    'n' => 'getoptp',
);

GetOptions (\%metaopt,
            'o=s',
            'l|long=s',
            'n=s');

shift @ARGV if ($ARGV[0] eq '--' ); ## This should be the end of getoptp's
                                    ## options indicator.

## The options to the caller's program
my %optshort;
my %optlong;
my @optctrl;
my %opt;

if ($metaopt{o}) {
    ## No commas between short options and some can have ':' so the split is
    ## more involved.
    my @split;
    for (split(//,$metaopt{o})) {
        $_ eq ':' ? $split[-1] .= ':' : push @split, $_;
    }
    %optshort = map { /(.*):/ ? ($1 => 's') : ($_ => 1)} @split;
    push @optctrl, map { s/:/=s/; $_} @split;
}

if ($metaopt{l}) {
    %optlong = map { /(.*):/ ? ($1 => 's') : ($_ => 1)} split(/,/, $metaopt{l});
    push @optctrl, map { s/:/=s/; $_} split(/,/, $metaopt{l});
}

GetOptions(\%opt,
           @optctrl);

my @out;
for (keys %opt) {
    my $optfmt;
    if (defined($optshort{$_})) {
        push @out, "-$_";
        $optfmt = $optshort{$_};
    } elsif (defined($optlong{$_})) {
        push @out, "--$_";
        $optfmt = $optlong{$_};
    } else {
        die "$metaopt{n}: $_ is not a short or long opt"
    }
    if ($optfmt eq 's') {
        push @out, $opt{$_};
    }
}
push @out, '--' if (@ARGV and $ARGV[0] ne '--' );
push @out, @ARGV;
push @out, '--' if (!@out or !@ARGV);
print "@out";
exit 0;
